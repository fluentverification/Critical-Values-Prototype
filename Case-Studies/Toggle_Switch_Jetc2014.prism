ctmc

const int aTc = 0;

const int IPTG = 0;

const int TetR_Change = 5;	//value at 5 gives "good" result
const int LacI_Change = 10;	//value at 10 gives "good" result

//formula p_t = 9.9 / ((LacI * .5) * (LacI * .5) + 1.99);
//formula p_l = 9.9 / ((TetR * .5) * (TetR * .5) + 1.99);

formula p_t = .99/(0.25*(LacI/(0.0496277915632754*IPTG + 1))*(LacI/(0.0496277915632754*IPTG + 1)) + 1.99);  //original formula from Jetc paper
formula p_l = .99/(0.25*(TetR/(0.0496277915632754*aTc + 1))*(TetR/(0.0496277915632754*aTc + 1)) + 1.99);

const int MAXLacI = 80;		//must be factor of LacI_change
const int MAXTetR = 50;		//must be factor of TetR_change


module TetR_def

        TetR : [0..MAXTetR] init 0;

	[] TetR=0 -> p_t * (10/TetR_Change) : (TetR'=TetR+TetR_Change);		//(10/TetR_Change) is based from the formula above being for every 10 molecules
										//divided by the number we are currently changing it by (TetR_Change)

    	[] TetR>0 & TetR<MAXTetR -> p_t * (10/TetR_Change) : (TetR'=TetR+TetR_Change) + 0.0075*TetR/TetR_Change:(TetR'=TetR-TetR_Change);
	[] TetR=MAXTetR -> 0.0075*TetR/TetR_Change : (TetR'=TetR-TetR_Change);
	
endmodule

module LacI_def

	LacI : [0..MAXLacI] init 60;

	[] LacI=0 -> p_l * (10/LacI_Change):(LacI'=LacI+LacI_Change);
    	[] LacI>0 & LacI<MAXLacI -> p_l * (10/LacI_Change):(LacI'=LacI+LacI_Change) + 0.0075*LacI/LacI_Change :(LacI'=LacI-LacI_Change);
	[] LacI=MAXLacI -> 0.0075*LacI/LacI_Change :(LacI'=LacI-LacI_Change);
	
endmodule